fov {z -3 y 1 x 2 lookat(0,-.3,0)} 
bg{L 0}

DEF MAXITER 6
MACRO SUBDIV min(5,MAXITER-ITER+1)
DEF SUBSZ .5
DEF REACH .7
DEF SUBSZ2 .6
DEF REACH2 .3


light {AMBIENT s .2}
light {PHONG specular 10 alpha 10 s 1 {RGB 0 1 1}
  point(1,-2, 2 ) }
light {PHONG specular 30 alpha 50 s 4 {RGB 1 0 0 }
  point(2,6, -2 ) }


scene{
  subcross{ s 1.6}
}
subcross{
  drawsubcross{}
  if (ITER < MAXITER - 1) {
    4*{rz 90}
      sub_{x REACH s SUBSZ PUSH subcross2 ry 45}
    2*{rx 180}
      sub_{z -REACH s SUBSZ PUSH subcross2 ry 45}
  }
}


subcross2{
  drawsubcross{}
  if (ITER < MAXITER - 1) {
    4*{rz 90}
      sub_{x REACH2 s SUBSZ2 PUSH subcross}
    2*{rx 180}
      sub_{z -REACH2 s SUBSZ2 PUSH subcross ry 45}
  }
}

sub_ {
  POP{
    DEF{ITER=ITER+1}}
}

drawsubcross{
  4*{rz 90} subsqu{y .5 rx 90 DEF{SSQ=SUBDIV}}
  2*{rx 180} subsqu{z -.5 DEF{SSQ=SUBDIV}}
}

subsqu{subsqu_{ 
  RGB #001010 
  H pow(abs(sin(x*100+y*2.5)),subdiv+3.5)*120}}
subsqu_{
  if (SSQ=0){5*{ry (saw(x*.5 + y*.1)*100) L .2 S -.1 s 1.01 .6 1 rz 60}CIRCLE{}}
  if (SSQ > 0) {
    2*{x .5}2*{y .5}subsqu{x -.25 y -.25 s .5 DEF{SSQ=SSQ-1}}
  }
}
