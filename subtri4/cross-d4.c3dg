bg{L -1}
fov {z -2 x -1.5 y 1 lookat(.3,0,.6)}
DEF MAXITER 5
MACRO SUBDIV min(4,MAXITER-ITER+1)
DEF SUBSZ .5
DEF REACH .75
DEF SUBSZ2 .5
DEF REACH2 .5


light {AMBIENT s .25}
light {PHONG specular 30 alpha 50 s 1.7 {RGB 1 1 1}
  point(
    -1,-8, -2
  )
}


scene{
  subcross{ s 1 shading misty_city_by_sea}
}
subcross{
  drawsubcross{}
  if (ITER < MAXITER - 1) {
    4*{rz 90}
      sub_{x REACH ry 45 s SUBSZ PUSH subcross2}
    2*{rx 180}
      sub_{z -REACH rx 45 s SUBSZ PUSH subcross2}
  }
}


subcross2{
  drawsubcross{}
  if (ITER < MAXITER - 1) {
    4*{rz 90}
      sub_{x REACH2 s SUBSZ2 PUSH subcross}
    2*{rx 180}
      sub_{z -REACH2 s SUBSZ2 PUSH subcross}
  }
}

sub_ {
  POP{
    DEF{ITER=ITER+1}}
}

drawsubcross{
  4*{rz 90} subsqu{y .5 rx 90 {SAT=1} DEF{SSQ=SUBDIV}}
  2*{rx 180} subsqu{z -.5 {SAT=1} DEF{SSQ=SUBDIV}}
}

subsqu{
  if (SSQ=0){5*{{SAT=SAT-.2}}SQUARE{col0 (z) rz rndf()*10 skewx 1000}}
  if (SSQ > 0) {
    2*{x .5}2*{y .5}subsqu{x -.25 y -.25 s .5 DEF{SSQ=SSQ-1}}
  }
}


shading misty_city_by_sea {
    point(0.0) { A 1
        RGB lirp(0,1,.5,.3,SAT) lirp(0,1,.5,.1,SAT) lirp(0,1,.5,0,SAT)}
    point(0.25) { A 1
        RGB lirp(0,1,.5,.4,SAT) lirp(0,1,.5,0,SAT) lirp(0,1,.5,.2,SAT) }
    point(.4) { A 1
        RGB lirp(0,1,.5,.1,SAT) lirp(0,1,.4,.2,SAT) lirp(0,1,.7,.3,SAT) }
    point(.75) { A 1
        RGB lirp(0,1,.5,.3,SAT) lirp(0,1,.5,.2,SAT) lirp(0,1,.5,.1,SAT) }
    point(1) { A 1
        RGB lirp(0,1,.5,1,SAT) lirp(0,1,.5,.7,SAT) lirp(0,1,1,1,SAT) }
}